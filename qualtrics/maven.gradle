apply plugin: 'maven'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}

group = 'com.rudderstack.android.integration'
archivesBaseName = 'qualtrics'
version = '1.0.0'

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://s01.oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Rudder-Integration-Qualtrics-Android'
                artifactId 'qualtrics'
                packaging 'aar'
                description 'RudderStack\'s Native SDK Integration support for qualtrics.'
                url 'https://github.com/rudderlabs/rudder-integration-qualtrics-android'

                scm {
                    connection 'scm:git:git://github.com/rudderlabs/rudder-integration-qualtrics-android'
                    developerConnection 'scm:git:ssh://github.com:rudderlabs/rudder-integration-qualtrics-android'
                    url 'https://github.com/rudderlabs/rudder-integration-qualtrics-android/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/rudderlabs/rudder-integration-qualtrics-android/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'desusai7'
                        name 'Desu Sai Venkat'
                        email 'venkat@rudderstack.com'
                    }
                }
            }
        }
    }
}